// Some code provided by Chuck Mortimore, Salesforce.com.
public class BatchMonitoring {  
    private String subject;

    // The Issuer must be the OAuth client_id or the connected app for which the developer registered their certificate.
    // from https://help.salesforce.com/articleView?id=remoteaccess_oauth_SAML_bearer_flow.htm&type=5
    private String issuer = '[TBD: REPLACE THIS WITH A VALID OAUTH_CLIENT_ID]';
    private String audience = 'https://login.salesforce.com';
    private String action = 'https://login.salesforce.com/services/oauth2/token'; 
    
    private string notBefore;
    private String notOnOrAfter;
    
    private String assertionId; 
    private string digest1;
    private string signature1;
    
    // Encoded key is an encoded version of a private key that you create to generate a certificate used with a Salesforce Connected App
    // https://developer.salesforce.com/blogs/isv/2015/04/integrating-multi-orgs-using-oauth.html
    private String encodedKey = '[TBD: REPLACE THIS WITH A VALID ENCODED KEY]';
    
    private String preCannonicalizedResponse = '<saml:Assertion xmlns:saml="urn:oasis:names:tc:SAML:2.0:assertion" ID="ASSERTION_ID" IssueInstant="NOT_BEFORE" Version="2.0"><saml:Issuer Format="urn:oasis:names:tc:SAML:2.0:nameid-format:entity">ISSUER</saml:Issuer><saml:Subject><saml:NameID Format="urn:oasis:names:tc:SAML:1.1:nameid-format:unspecified">SUBJECT</saml:NameID><saml:SubjectConfirmation Method="urn:oasis:names:tc:SAML:2.0:cm:bearer"><saml:SubjectConfirmationData NotOnOrAfter="NOT_ON_OR_AFTER" Recipient="RECIPIENT"></saml:SubjectConfirmationData></saml:SubjectConfirmation></saml:Subject><saml:Conditions NotBefore="NOT_BEFORE" NotOnOrAfter="NOT_ON_OR_AFTER"><saml:AudienceRestriction><saml:Audience>AUDIENCE</saml:Audience></saml:AudienceRestriction></saml:Conditions><saml:AuthnStatement AuthnInstant="NOT_BEFORE"><saml:AuthnContext><saml:AuthnContextClassRef>urn:oasis:names:tc:SAML:2.0:ac:classes:unspecified</saml:AuthnContextClassRef></saml:AuthnContext></saml:AuthnStatement></saml:Assertion>';
    private String preCannonicalizedSignedInfo = '<ds:SignedInfo xmlns:ds="http://www.w3.org/2000/09/xmldsig#"><ds:CanonicalizationMethod Algorithm="http://www.w3.org/2001/10/xml-exc-c14n#"></ds:CanonicalizationMethod><ds:SignatureMethod Algorithm="http://www.w3.org/2000/09/xmldsig#rsa-sha1"></ds:SignatureMethod><ds:Reference URI="#ASSERTION_ID"><ds:Transforms><ds:Transform Algorithm="http://www.w3.org/2000/09/xmldsig#enveloped-signature"></ds:Transform><ds:Transform Algorithm="http://www.w3.org/2001/10/xml-exc-c14n#"></ds:Transform></ds:Transforms><ds:DigestMethod Algorithm="http://www.w3.org/2000/09/xmldsig#sha1"></ds:DigestMethod><ds:DigestValue>DIGEST</ds:DigestValue></ds:Reference></ds:SignedInfo>';
    private String signatureBlock = '<ds:Signature xmlns:ds="http://www.w3.org/2000/09/xmldsig#">SIGNED_INFO<ds:SignatureValue>SIGNATURE_VALUE</ds:SignatureValue></ds:Signature><saml:Subject>';
    
    private Map<String,Object> connection;

    public BatchMonitoring() {
        // This should be a valid Username for the Business org
        subject = '[TBD: REPLACE WITH A VALID USERNAME FOR BUSINESS ORG]';
        
        datetime nowDt = datetime.now();
        Long nowLong = nowDt.getTime();
        datetime notBeforeDt = datetime.newInstance(nowLong - 120000);
        
        notBefore = notBeforeDt.formatGmt('yyyy-MM-dd\'T\'HH:mm:ss') + 'Z';
        datetime notOnOrAfterDt = nowDt.addMinutes(5);
        notOnOrAfter = notOnOrAfterDt.formatGmt('yyyy-MM-dd\'T\'HH:mm:ss') + 'Z';
        
        Double random = Math.random();
        assertionId = EncodingUtil.convertToHex(Crypto.generateDigest('SHA256',  Blob.valueOf('assertion' + random)));
    }
    
    public String getResult(){       
        preCannonicalizedResponse = preCannonicalizedResponse.replaceAll('ASSERTION_ID',assertionId);
        preCannonicalizedResponse = preCannonicalizedResponse.replaceAll('ISSUER',issuer);
        preCannonicalizedResponse = preCannonicalizedResponse.replaceAll('AUDIENCE',audience);
        preCannonicalizedResponse = preCannonicalizedResponse.replaceAll('RECIPIENT',action);
        preCannonicalizedResponse = preCannonicalizedResponse.replaceAll('SUBJECT',subject);
        preCannonicalizedResponse = preCannonicalizedResponse.replaceAll('NOT_BEFORE',notBefore);
        preCannonicalizedResponse = preCannonicalizedResponse.replaceAll('NOT_ON_OR_AFTER',notOnOrAfter);
        
        //Prepare the Digest
        Blob digest = Crypto.generateDigest('SHA1',  Blob.valueOf(preCannonicalizedResponse));
        String digestString = EncodingUtil.base64Encode(digest);
        
        //Prepare the SignedInfo
        preCannonicalizedSignedInfo = preCannonicalizedSignedInfo.replaceAll('ASSERTION_ID',assertionId);
        preCannonicalizedSignedInfo = preCannonicalizedSignedInfo.replaceAll('DIGEST',digestString);
        
        //Sign the SignedInfo
        Blob privateKey = EncodingUtil.base64Decode(encodedKey);
        Blob input = Blob.valueOf(preCannonicalizedSignedInfo);
        
        Blob signature = Crypto.sign('RSA', input, privateKey);
        //Blob signature = Blob.valueOf('foo');
        String signatureString = EncodingUtil.base64Encode(signature);
        
        //Prepare the signature block
        signatureBlock = signatureBlock.replaceAll('SIGNED_INFO',preCannonicalizedSignedInfo);
        signatureBlock = signatureBlock.replaceAll('SIGNATURE_VALUE',signatureString);
        
        //cheap trick for enveloping the signature by swapping out last element with sig + last elements of whole message
        preCannonicalizedResponse = preCannonicalizedResponse.replaceAll('<saml:Subject>', signatureBlock );
        
        //return the prefix + our prepped message
        System.debug('ASSERTION:' + preCannonicalizedResponse);
        return preCannonicalizedResponse;
        
    }
    
    public String base64URLencode(Blob input){ 
        String output = encodingUtil.base64Encode(input);
        output = output.replace('+', '-');
        output = output.replace('/', '_');
        while ( output.endsWith('=')){
            output = output.subString(0,output.length()-1);
        }
        return output;
    }
 
    public Map<String,Object> authenticate()
    {
        String saml = getResult();
        http h = new http();
        httpRequest req = new httpRequest();
        req.setHeader('Content-Type','application/x-www-form-urlencoded');
        req.setMethod('POST');
        String param = 'grant_type=' + encodingUtil.urlEncode('urn:ietf:params:oauth:grant-type:saml2-bearer','UTF-8')+'&assertion=' + base64URLencode(blob.valueOf(saml)); 
        req.setBody(param);
        
        req.setEndpoint(action);
        httpResponse res = h.send(req);
        System.debug('\n\nRESPONSE: ' + res.getBody());
        connection = (Map<String,Object>) JSON.deserializeUntyped(res.getBody());
        return connection;
    }

    public void batch() {
        batch('day');
    }

    public void batch(String batch) {
        authenticate();
        if (batch=='day') {
            Database.executeBatch(new DailyBatch(this), 1); // batch size = 1
        } else {
            Database.executeBatch(new ReconciliationBatch(this), 1); // batch size = 1
        }
    }

    public String createJobId(){
        return '' + Math.mod(Math.round(Math.random()*1000), 1000);
    }
      
    public String logEvent(String jobType, String jobId, String eventType, DateTime ts) {
    return logEvent(connection, jobType, jobId, eventType, ts);
    }
    
    public String logEvent(Map<String,Object> connection, String jobType, String jobId, String eventType, DateTime ts) {
        String key = UserInfo.getOrganizationId() + '+' + jobType;
        String formatted = ts.formatGMT('yyyy-MM-dd\'T\'HH:mm:ssZ');
        Map<String,String> data = new Map<String,String> {
            'OrgID__c' => UserInfo.getOrganizationId(),
                'JobType__c' => jobType,
                'EventType__c' => eventType,  
                eventType + 'TS__c' => formatted,
                eventType + 'JobId__c' => jobId,
                'JobID__c' => jobId 
                };
        String json = JSON.serialize(data);
        http h = new http();
        httpRequest req = new httpRequest();
        req.setHeader('content-type', 'application/json');
        req.setHeader('Authorization', 'Bearer ' + connection.get('access_token'));      
        req.setMethod('POST');
        req.setBody(json);
        
        req.setEndpoint(connection.get('instance_url') + '/services/data/v20.0/sobjects/MonitoringEvent__c/JobKey__c/' + key + '?_HttpMethod=PATCH');
        httpResponse res = h.send(req);
        return key;
    }
}
